apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

task sourcesJar(type: Jar) {
    from project.android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadoc(type: Javadoc) {
//    excludes = ['**/*.kt'] // < ---- Exclude all kotlin files from javadoc file.
    setFailOnError false
    source = project.android.sourceSets.main.java.srcDirs
    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
    def files = new File(projectDir.getPath() + File.separator + "libs" + File.separator).listFiles()
    def absolutePathArray = new String[files.length]
    for (int i = 0; i < files.length; i++) {
        absolutePathArray[i] = files[i].absolutePath
    }
    classpath += project.files(absolutePathArray)
    classpath += configurations.compile
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task dokkaJavadoc(type: dokka.class) {
    outputFormat = "javadoc"
    outputDirectory = javadoc.destinationDir
}

task generateJavadoc(type: Jar, dependsOn: dokkaJavadoc) {
    group = 'jar'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

version = "0.1.3"

def siteUrl = 'https://github.com/atulgpt/RxMarkdown'
def gitUrl = 'https://github.com/atulgpt/RxMarkdown.git'

group = "com.yydcdut"

if (project.rootProject.file('local.properties').exists()) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    bintray {
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")
        configurations = ['archives']
        pkg {
            repo = "maven"
            name = "Markdown-Processor"
            websiteUrl = siteUrl
            vcsUrl = gitUrl
            licenses = ["Apache-2.0"]
            publish = true
        }
    }

}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'Markdown-Processor'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'yydcdut'
                        name 'yydcdut'
                        email '378040621@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
